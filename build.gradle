import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group 'io.github.apickledwalrus'
version '1.3-alpha3'

compileJava.options.encoding = 'UTF-8'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    implementation group: 'com.google.code.findbugs', name: 'findbugs', version: '3.0.1'
    implementation group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.5-R0.1-SNAPSHOT'
    //implementation (group: 'com.github.SkriptLang.Skript', name: 'Skript', version: '2.6-beta2') {
    //    transitive = false
    //}
    implementation ('com.github.APickledWalrus:Skript:f2af79e67d') { // Temporary for Section API expansion PR
        transitive = false
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            'version': version
    ]
}

task nightlyResources(type: ProcessResources) {
    from 'src/main/resources', {
        include '**'
        filter ReplaceTokens, tokens: [
                'version' : project.property('version') + '-nightly-' + 'git rev-parse --short HEAD'.execute().text.trim()
        ]
    }
    into 'build/resources/main'
}

task nightlyBuild(type: Jar) {
    dependsOn nightlyResources
    from sourceSets.main.output
    archiveFileName = 'skript-gui-nightly.jar'
}
